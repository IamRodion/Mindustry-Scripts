# Enlaza una unidad de tipo "mono" para trabajar con la fábrica
ubind @mono
# Crea variables para los estados de la unidad trabajadora. 81X significa "ground factory" y "X" es el estado.
set takeFlag 811
set dropFlag 812
set stopFlag 813
# Obtiene las coordenadas de la fábrica y del núcleo
sensor fX factory1 @x
sensor fY factory1 @y
ulocate building core false @copper coreX coreY coreFound core
# Sí no se encuentra ningún núcleo se vuelve a intentar enlazar una unidad
jump 0 equal coreFound false
# Inicializa la bandera de la unidad en estado "stop"
ucontrol flag stopFlag 0 0 0 0
# Lee el estado actual de la unidad
sensor unitFlag @unit @flag
# Lee la configuración de la fábrica (el tipo de unidad a fabricar)
sensor factoryConfig factory1 @config
# Define las cantidades necesarias de recursos según el tipo de unidad a fabricar teniendo como prioridad el silicio
select coalAmountConfig equal factoryConfig @crawler 10 0
select titaniumAmountConfig equal factoryConfig @nova 20 0
select leadAmountConfig equal factoryConfig @dagger 10 0
select leadAmountConfig equal factoryConfig @nova 20 leadAmountConfig
select siliconAmountConfig equal factoryConfig @dagger 10 8
select siliconAmountConfig equal factoryConfig @nova 30 siliconAmountConfig
# Detecta las cantidades actuales de recursos en la fábrica y las almacena en variables
sensor coalAmount factory1 @coal
sensor titaniumAmount factory1 @titanium
sensor leadAmount factory1 @lead
sensor siliconAmount factory1 @silicon
# Calcula las cantidades de recursos que faltan para fabricar la unidad deseada
op sub coalAmountNeed coalAmountConfig coalAmount
op sub titaniumAmountNeed titaniumAmountConfig titaniumAmount
op sub leadAmountNeed leadAmountConfig leadAmount
op sub siliconAmountNeed siliconAmountConfig siliconAmount
# Calcula sí se deben omitir ciertos recursos si ya hay suficientes en la fábrica
select skipCoal greaterThanEq coalAmount coalAmountConfig true false
select skipTitanium greaterThanEq titaniumAmount titaniumAmountConfig true false
select skipLead greaterThanEq leadAmount leadAmountConfig true false
select skipSilicon greaterThanEq siliconAmount siliconAmountConfig true false
op and skipCT skipCoal skipTitanium
op and skipSL skipSilicon skipLead
# Si todos los recursos son suficientes, se establece la variable "skip" en true
op and skip skipCT skipSL
# Establece el ore necesario en la variable "ore" y sí no se necesita nada, la establece en "nothing" de color verde, teniendo el silicio como prioridad
select ore equal skipCoal true "[green]nothing[]" @coal
select ore equal skipTitanium true ore @titanium
select ore equal skipLead true ore @lead
select ore equal skipSilicon true ore @silicon
# Establece la cantidad de ore necesaria en la variable "oreAmount" teniendo en cuenta el ore establecido en la variable "ore". Si no se necesita nada, la cantidad será 0.
select oreAmount equal ore @coal coalAmountNeed 0
select oreAmount equal ore @titanium titaniumAmountNeed oreAmount
select oreAmount equal ore @lead leadAmountNeed oreAmount
select oreAmount equal ore @silicon siliconAmountNeed oreAmount
# Establece un texto que representa el estado actual de la unidad basado en la variable "unitFlag"
select textUnitFlag equal unitFlag 811 "[red]Take[]" "[green]Idle[]"
select textUnitFlag equal unitFlag 812 "[yellow]Drop[]" textUnitFlag
# Muestra un mensaje con el tipo de unidad trabajadora, su estado actual, la configuración de la fábrica, el recurso necesario y la cantidad necesaria
print "[grey]Unit Type:[] {0}\n[grey]Unit Mode:[] {1} ({2})\n[grey]Factory Mode:[] {3}\n[grey]Factory Need:[] [red]{4}[] ({5})"
format @unit
format textUnitFlag
format unitFlag
format factoryConfig
format ore
format oreAmount
printflush message1
# Sí el estado de la unidad es "drop", salta a la sección de "dejar recursos"
jump 63 equal unitFlag dropFlag
# Sí no se necesitan recursos, se establece el estado de la unidad en "stop", de lo contrario se establece en "take"
select newFlag equal skip true stopFlag takeFlag
ucontrol flag newFlag 0 0 0 0
# Sí el estado de la unidad es "take", salta a la sección "obtener recursos"
jump 56 equal unitFlag takeFlag
# Sí no se cumplen ninguna de las condiciones anteriores, se detiene la unidad y se salta al inicio
ucontrol stop 0 0 0 0 0
jump 9 always
# Obtener recursos
ucontrol approach coreX coreY 1 0 0
ucontrol within coreX coreY 1 nearCore 0
jump 56 equal nearCore false
ucontrol itemDrop core 999
ucontrol itemTake core ore oreAmount 0 0
ucontrol flag dropFlag 0 0 0 0
jump 9 always
# Dejar recursos
ucontrol approach fX fY 1 0 0
ucontrol within fX fY 1 nearFactory 0
jump 63 equal nearFactory false
ucontrol itemDrop factory1 oreAmount 0 0 0
ucontrol flag stopFlag 0 0 0 0
jump 9 always
