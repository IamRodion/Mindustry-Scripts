# Enlaza una unidad de tipo "mono" para trabajar con la fábrica
ubind @mono
# Crea variables para los estados de la unidad trabajadora. 83X significa "naval factory" y "X" es el estado.
set takeFlag 831
set dropFlag 832
set stopFlag 833
# Obtiene las coordenadas de la fábrica y del núcleo
sensor fX factory1 @x
sensor fY factory1 @y
ulocate building core false @copper coreX coreY coreFound core
# Sí no se encuentra ningún núcleo se vuelve a intentar enlazar una unidad
jump 0 equal coreFound false
# Inicializa la bandera de la unidad en estado "stop"
ucontrol flag stopFlag 0 0 0 0
# Lee el estado actual de la unidad
sensor unitFlag @unit @flag #9
# Lee la configuración de la fábrica (el tipo de unidad a fabricar)
sensor factoryConfig factory1 @config
# Define las cantidades necesarias de recursos según el tipo de unidad a fabricar teniendo como prioridad el silicio
select titaniumAmountConfig equal factoryConfig @retusa 20 0
select metaglassAmountConfig equal factoryConfig @risso 35 0
select siliconAmountConfig equal factoryConfig @retusa 15 0
select siliconAmountConfig equal factoryConfig @risso 20 siliconAmountConfig
# Detecta las cantidades actuales de recursos en la fábrica y las almacena en variables
sensor titaniumAmount factory1 @titanium
sensor metaglassAmount factory1 @metaglass
sensor siliconAmount factory1 @silicon
# Calcula las cantidades de recursos que faltan para fabricar la unidad deseada
op sub titaniumAmountNeed titaniumAmountConfig titaniumAmount
op sub metaglassAmountNeed metaglassAmountConfig metaglassAmount
op sub siliconAmountNeed siliconAmountConfig siliconAmount
# Calcula sí se deben omitir ciertos recursos si ya hay suficientes en la fábrica
select skipTitanium greaterThanEq titaniumAmount titaniumAmountConfig true false
select skipMetaglass greaterThanEq metaglassAmount metaglassAmountConfig true false
select skipSilicon greaterThanEq siliconAmount siliconAmountConfig true false
op and skipTM skipTitanium skipMetaglass
# Si todos los recursos son suficientes, se establece la variable "skip" en true
op and skip skipTM skipSilicon
# Establece el ore necesario en la variable "ore" y sí no se necesita nada, la establece en "nothing" de color verde, teniendo el silicio como prioridad
select ore equal skipTitanium true "[green]nothing[]" @titanium
select ore equal skipMetaglass true ore @metaglass
select ore equal skipSilicon true ore @silicon
# Establece la cantidad de ore necesaria en la variable "oreAmount" teniendo en cuenta el ore establecido en la variable "ore". Si no se necesita nada, la cantidad será 0.
select oreAmount equal ore @titanium titaniumAmountNeed 0
select oreAmount equal ore @metaglass metaglassAmountNeed oreAmount
select oreAmount equal ore @silicon siliconAmountNeed oreAmount
# Establece un texto que representa el estado actual de la unidad basado en la variable "unitFlag"
select textUnitFlag equal unitFlag 831 "[red]Take[]" "[green]Idle[]"
select textUnitFlag equal unitFlag 832 "[yellow]Drop[]" textUnitFlag
# Muestra un mensaje con el tipo de unidad trabajadora, su estado actual, la configuración de la fábrica, el recurso necesario y la cantidad necesaria
print "[grey]Unit Type:[] {0}\n[grey]Unit Mode:[] {1} ({2})\n[grey]Factory Mode:[] {3}\n[grey]Factory Need:[] [red]{4}[] ({5})"
format @unit
format textUnitFlag
format unitFlag
format factoryConfig
format ore
format oreAmount
printflush message1
# Sí el estado de la unidad es "drop", salta a la sección de "dejar recursos"
jump 55 equal unitFlag dropFlag
# Sí no se necesitan recursos, se establece el estado de la unidad en "stop", de lo contrario se establece en "take"
select newFlag equal skip true stopFlag takeFlag
ucontrol flag newFlag 0 0 0 0
# Sí el estado de la unidad es "take", salta a la sección "obtener recursos"
jump 48 equal unitFlag takeFlag
# Sí no se cumplen ninguna de las condiciones anteriores, se detiene la unidad y se salta al inicio
ucontrol stop 0 0 0 0 0
jump 9 always
# Obtener recursos
ucontrol approach coreX coreY 1 0 0 #48
ucontrol within coreX coreY 1 nearCore 0
jump 48 equal nearCore false
ucontrol itemDrop core 999
ucontrol itemTake core ore oreAmount 0 0
ucontrol flag dropFlag 0 0 0 0
jump 9 always
# Dejar recursos
ucontrol approach fX fY 1 0 0 #55
ucontrol within fX fY 1 nearFactory 0
jump 55 equal nearFactory false
ucontrol itemDrop factory1 oreAmount 0 0 0
ucontrol flag stopFlag 0 0 0 0
jump 9 always
